# This file is part of emu.
# emu is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# any later version.
# emu is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# You should have received a copy of the GNU General Public License
# along with emu. If not, see <https://www.gnu.org/licenses/>.

program := declaration* EOF

declaration := structDeclaration
             | funcDeclaration
             | varDeclaration
             | statement

decoratedIdentifier := modifier* IDENTIFIER ": " decoratedType
decoratedType := "*"* TYPE "[" expression "]"*

structDeclaration := modifier* "struct" IDENTIFIER "{" parameters "};"
funcDeclaration := modifier* "func" IDENTIFIER "(" parameters? ")" ( ": " decoratedType )? block
varDeclaration := decoratedIdentifier "=" expression ";"
parameters := decoratedIdentifier ( "," decoratedIdentifier )*

arguments := (expression ( "," expression )*)?

statement := exprStatement
           | ifElseStatement
           | whileStatement
           | forStatement
           | switchStatement
           | caseStatement
           | returnStatement
           | block

block := "{" declaration* "}"

exprStatement := expression ";"
ifElseStatement := "if" "(" expression ")" statement ( "else" statement )?
whileStatement := "while" "(" expression ")" statement
forStatement := "for" "(" statement expression ";" expression ")" statement
switchStatement := "switch" "(" expression ")" statement
caseStatement := "case" expression ":" statement
returnStatement := "return" expression? ";"
breakStatement := "break;"
continueStatement := "continue;"

modifier := "pure" | "const" | "inline" | "comptime" | "register" | "restrict"

expression := assignment
assignment := logicOr ( "=" | "+=" | "-=" | "*=" | "/=" | "%=" | "<<=" | ">>=" | "&=" | "^=" | "|=" logicOr )*
logicOr := ( logicXor "||" )* logicXor
logicXor := ( logicAnd "^^" )* logicAnd
logicAnd := ( bitwiseOr "&&" )* bitwiseOr
bitwiseOr := ( bitwiseXor "|" )* bitwiseXor
bitwiseXor := ( bitwiseAnd "^" )* bitwiseAnd
bitwiseAnd := ( equality "&" )* equality
equality := ( comparison ( "==" | "!=" ) )* comparison
comparison := ( shift ( ">" | ">=" | "<" | "<=" ) )* shift
shift := ( term ( "<<" | ">>" ) )* term
term := ( factor ( "+" | "-" ) )* factor
factor := ( prefix ( "*" | "/" | "%" ) )* prefix
prefix := ( "++" | "--" | "+" | "-" | "!" | "~" | "*" | "&" | "(" decoratedType ")" )* postfix
postfix := primary ( "++" | "--" | "(" arguments ")" | "[" arguments "]" | "." IDENTIFIER | "->" IDENTIFIER )*
primary := "true" | "false" | "undefined" | NUMBER | "\'" ANY "\'" | STRING | IDENTIFIER | "(" expression ")" | "{" arguments "}"

NUMBER := DIGIT+ ( "." DIGIT+ )?
STRING := "\"" ANY* "\""
IDENTIFIER := ALPHA ( ALPHA | DIGIT )*
ALPHA := "a" ... "z" | "A" ... "Z" | "_"
DIGIT := "0" ... "9"
TYPE := "bool" | "u8" | "u16" | "u32" | "u64" | "i8" | "i16" | "i32" | "i64" | "f16" | "f32" | "f64"
